500 'VOCAB Ichigo-Tango Eng-Jpn Oliver Kaltstein
510 GSB1100:S=0:Q=20:FORI=1TOQ:GSB1001+RND(L):CLS:?:?" L E A R N   V O C A B U L A R Y"
520 LC2,6:?"ENGLISH         JAPANESE":LC0,11:?"ENGLISH   : ";STR$(A):?:?:?"JAPANESE  ";
530 INPUT"? ",Z:GSB600:IFETHENS=S+1:LED0:PLAY"ED":?:?"CORRECTION: ";STR$(B):WAIT300ELSEPLAY"CB"
540 NEXT
550 LC0,20:IFS=0THENLED1:PLAY"CEG<C":?"P E R F E C T !!   NO ERRORS !!"ELSE?"UNFORTUNATELY",S*100/Q;"%  WERE WRONG" 
560 ?:?"     ANY KEY TO CONTINUE"
570 K=INKEY():IFK=0GOTO570
580 GOTO500
600 E=0:FORJ=0TOLEN(B)-1:IFPEEK(B+J)<>VPEEK(12+J,14)THENE=1
610 NEXT:IFVPEEK(12+LEN(B),14)<>0THENE=1
620 RTN
1001 A="one":B="ichi":RTN
1002 A="two":B="ni":RTN
1003 A="three":B="san":RTN
1004 A="big":B="ookii":RTN
1005 A="apple":B="ringo":RTN
1006 A="to see":B="miru":RTN
1007 A="strawberry":B="ichigo":RTN
1008 A="school":B="gakkou":RTN
1009 A="cat":B="neko":RTN
1010 A="you":B="anata":RTN
1011 A="old":B="furui":RTN
1100 L=11:RTN:***DON'T FORGET TOPUT THE LENGTH OF YOUR LIST HERE
